(ns kotoba.geo.kotoba-golden-test
  (:require [clojure.java.shell :as shell]
            [clojure.string :as str]
            [clojure.test :refer [deftest is]]
            [kotoba.compiler.core :as compiler]
            [kotoba.compiler.ir :as ir]
            [kotoba.geo.math :as math]
            [kotoba.geo.projection :as projection]))

(defn- sphere [lng lat]
  (let [lng-rad (math/to-radians lng)
        lat-rad (math/to-radians lat)
        cos-lat (math/cos lat-rad)]
    [(* cos-lat (math/sin lng-rad))
     (math/sin lat-rad)
     (- (* cos-lat (math/cos lng-rad)))]))

(defn- orientation-oracle []
  (let [a (sphere 139.0 36.0) q (sphere 140.0 35.0) b (sphere 139.0 35.0)
        u (mapv - q a) v (mapv - b a)
        cross [(- (* (u 1) (v 2)) (* (u 2) (v 1)))
               (- (* (u 2) (v 0)) (* (u 0) (v 2)))
               (- (* (u 0) (v 1)) (* (u 1) (v 0)))]]
    (reduce + (map * cross a))))

(deftest projection-and-globe-mesh-goldens-agree-across-targets
  (let [source (slurp "src/kotoba/geo_golden.kotoba")
        names ['tokyo-world-x 'tokyo-world-y 'tokyo-roundtrip-lng
               'tokyo-roundtrip-lat 'globe-triangle-orientation]
        point (projection/lng-lat 139.7671 35.6812)
        world (projection/lng-lat->world-px point 12.0)
        roundtrip (projection/world-px->lng-lat world 12.0)
        expected [(:x world) (:y world) (:lng roundtrip) (:lat roundtrip)
                  (orientation-oracle)]
        js-artifact (compiler/compile-source source :js-kotoba-v1)
        wasm-artifact (compiler/compile-source source :wasm32-browser-kotoba-v1)
        reference (mapv #(ir/execute (:kir js-artifact) % []) names)
        js64 (.encodeToString (java.util.Base64/getEncoder)
                              (.getBytes ^String (:source js-artifact) "UTF-8"))
        wasm64 (.encodeToString (java.util.Base64/getEncoder) (:bytes wasm-artifact))
        expected-js (str "[" (str/join "," (map #(Double/toString (double %)) expected)) "]")
        names-js (str "[" (str/join "," (map #(str "\"" % "\"") names)) "]")
        node-source
        (str "const expected=" expected-js ",names=" names-js ";"
             "const close=(a,b)=>Math.abs(a-b)<=1e-9;"
             "Promise.all([import('data:text/javascript;base64," js64 "'),"
             "WebAssembly.instantiate(Buffer.from('" wasm64 "','base64'),{})]).then(([j,w])=>{"
             "const a=j.instantiateKotoba({}),b=w.instance.exports;"
             "const js=names.map(n=>a[n]()),wa=names.map(n=>b[n]());"
             "if(!js.every((v,i)=>close(v,expected[i])&&Object.is(v,wa[i])))process.exit(2);"
             "}).catch(e=>{console.error(e);process.exit(99)})")
        node-result (shell/sh "node" "--input-type=module" "-e" node-source)]
    (is (every? true? (map #(< (Math/abs (- %1 %2)) 1.0e-9) reference expected)))
    (is (pos? (last reference)) "globe patch triangle faces outward")
    (is (zero? (:exit node-result)) (:err node-result))
    (is (= :kotoba.floating-point/ieee-754-f32-f64-v7
           (:floating-point-policy js-artifact)))
    (is (= #{} (set (:effects (:kir js-artifact)))))))
